\{ "OpenAI API key" } ->

let key = .'OpenAI API key'

let concatSep = import github
      { owner: "Gabriella439"
      , repository: "grace"
      , path: "prelude/text/concatSep.ffg"
      }

let generatePoem idea =
        let structure = prompt
                { key
                , text: "
                    Plan the structure of a new poem based on this idea:

                    ${idea}

                    Describe its form, stanza count, lines per stanza, and rhyme scheme.
                    "
                }

        let renderedStructure = "
                - Form: ${structure."Form"}
                - Stanzas: ${show (structure."Stanza Count" : Natural)}
                - Lines per stanza: ${show (structure."Lines per Stanza" : Natural)}
                - Rhyme scheme: ${structure."Rhyme Scheme"}
                "

        let style = prompt
                { key
                , text: "
                    Given this poem structure:

                    ${renderedStructure}

                    Choose a poetic style: tone, voice, and literary devices to emphasize.
                    "
                }

        let renderedStyle = "
                - Tone: ${style."Tone"}
                - Voice: ${style."Voice"}
                - Literary Devices: ${concatSep ", " style."Literary Devices"}
                "

        let poem = prompt
                { key
                , text: "
                    Write a complete poem based on this idea:

                    ${idea}

                    Structure:

                    ${renderedStructure}

                    Style:

                    ${renderedStyle}
                    "
                }

        in { structure, style, poem }

in  generatePoem "The stillness of early morning"
